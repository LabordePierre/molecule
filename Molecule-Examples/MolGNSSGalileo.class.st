Class {
	#name : #MolGNSSGalileo,
	#superclass : #MolAbstractComponentImpl,
	#traits : 'MolGPSData + MolGPSDataServices',
	#classTraits : 'MolGPSData classTrait + MolGPSDataServices classTrait',
	#instVars : [
		'accuracy',
		'sendCurrentPositionThread'
	],
	#category : #'Molecule-Examples-GPS Example'
}

{ #category : #services }
MolGNSSGalileo >> accuracy [

	^ accuracy
]

{ #category : #services }
MolGNSSGalileo >> accuracy: anObject [

	accuracy := anObject
]

{ #category : #services }
MolGNSSGalileo >> componentActivate [
	"Start a thread to simulate sending of the geo position and accuracy precision each second"

	sendCurrentPositionThread := [
	                             [ true ] whileTrue: [
		                             (Delay forMilliseconds: 50) wait.
		                             self getMolGPSDataEventsNotifier
			                             currentPositionChanged:
			                             self getRandomizedGNSSGalileoPosition.
		                             self increaseAccuracy ] ] forkAt:
		                             Processor userBackgroundPriority
]

{ #category : #services }
MolGNSSGalileo >> componentInitialize [
	"Set starting accuracy"

	self accuracy: 1000
]

{ #category : #services }
MolGNSSGalileo >> componentPassivate [

	sendCurrentPositionThread ifNotNil: [ :e | e terminate ].
	sendCurrentPositionThread := nil
]

{ #category : #services }
MolGNSSGalileo >> getAccuracyRadiusInMeters [
	"Get and return the accuracy of the GPS depending quality of signal and quantity of connected satellites"

	^ self accuracy
]

{ #category : #'component accessing' }
MolGNSSGalileo >> getMolGPSDataEventsNotifier [
	^self eventsNotifiers at: MolGPSDataEvents ifAbsent: [^MolNotFoundEventsNotifier new interface: MolGPSDataEvents name: nil].
]

{ #category : #services }
MolGNSSGalileo >> getRandomizedGNSSGalileoPosition [

	| random |
	random := Random new.
	^ (random next) * 2 @ (random next) * 2
]

{ #category : #services }
MolGNSSGalileo >> increaseAccuracy [

	| nextAccuracy |
	self accuracy > 1 ifTrue: [
		nextAccuracy := self accuracy - (0.1 * self accuracy). "10% better precision at each times"
		nextAccuracy < 1 ifTrue: [ "stay to 1" nextAccuracy := 1 ].
		self accuracy: nextAccuracy ]
]
